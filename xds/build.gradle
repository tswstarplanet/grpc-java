// Add dependency on the protobuf plugin
buildscript {
    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath libraries.protobuf_plugin
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

description = "gRPC: XDS plugin"

[compileJava].each() {
    it.options.compilerArgs += [
        // valueOf(int) in RoutingPriority has been deprecated
        "-Xlint:-deprecation",
    ]
}

dependencies {
    compile project(':grpc-protobuf'),
            project(':grpc-stub')
    compile (libraries.protobuf_util) {
        // prefer 2.3.2 from libraries instead of 2.1.3 from guava
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer 1.17 from libraries instead of 1.14 from guava
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }

    testCompile project(':grpc-core').sourceSets.test.output

    compileOnly libraries.javax_annotation
    
    testCompile project(':grpc-testing')
    signature "org.codehaus.mojo.signature:java17:1.0@signature"
}

apply plugin: 'com.google.protobuf'
sourceSets {
    main {
        proto {
            srcDir 'third_party/envoy/src/main/proto'
            srcDir 'third_party/protoc-gen-validate/src/main/proto'
        }
    }
}

configureProtoCompilation()

jar {
    classifier = 'original'
}

shadowJar {
    classifier = null
    dependencies {
        include(project(':grpc-xds'))
    }
    relocate 'io.envoyproxy', 'io.grpc.xds.shaded.io.envoyproxy'
    relocate 'validate', 'io.grpc.xds.shaded.com.lyft.pgv.validate'
    exclude "**/*.proto"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact shadowJar
            artifacts.removeAll { it.classifier == 'original' }
        }
    }
}
