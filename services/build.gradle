// Add dependency on the protobuf plugin
buildscript {
    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    }
    dependencies { classpath libraries.protobuf_plugin }
}

description = "gRPC: Services"

[compileJava].each() {
    // v1alpha of reflection.proto is deprecated at the file level.
    // Without this workaround, the project can not compile.
    it.options.compilerArgs += [
        "-Xlint:-deprecation",
    ]
}

dependencies {
    compile project(':grpc-protobuf'),
            project(':grpc-stub')
    compile (libraries.protobuf_util) {
        // prefer 2.3.2 from libraries instead of 2.1.3 from guava
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // prefer 1.17 from libraries instead of 1.14 from guava
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    compile libraries.re2j

    compileOnly libraries.javax_annotation
    testCompile project(':grpc-testing'),
            libraries.netty_epoll, // for DomainSocketAddress
            project(':grpc-core').sourceSets.test.output  // for FakeClock
    testCompileOnly libraries.javax_annotation
    signature "org.codehaus.mojo.signature:java17:1.0@signature"
}

configureProtoCompilation()
